apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

wrapper {
    gradleVersion = '4.10.2'
}

repositories {
    mavenCentral()
}

group = "io.github.benkiefer"
version = "5.0.1-SNAPSHOT"

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'org.testng:testng:6.9.4'
}

test {
    useTestNG()
}

install.dependsOn 'build'

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Ember Cli Gradle Plugin'
                packaging 'jar'
                description 'A plugin for building Ember CLI projects with gradle.'
                url 'https://github.com/benkiefer/ember-cli-gradle-plugin'

                scm {
                    connection 'git@github.com:benkiefer/ember-cli-gradle-plugin.git'
                    developerConnection 'git@github.com:benkiefer/ember-cli-gradle-plugin.git'
                    url 'https://github.com/benkiefer/ember-cli-gradle-plugin.git'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/benkiefer/ember-cli-gradle-plugin/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'benkiefer'
                        name 'Ben Kiefer'
                        email 'ben.a.kiefer@gmail.com'
                    }
                }
            }
        }
    }
}